#
# Copyright (c) 2023 Nordic Semiconductor ASA
#
# SPDX-License-Identifier: LicenseRef-Nordic-5-Clause
#

cmake_minimum_required(VERSION 3.20.0)

find_package(Zephyr REQUIRED HINTS $ENV{ZEPHYR_BASE})
project(network_module_test)

test_runner_generate(src/main.c)

target_sources(app
  PRIVATE
  src/main.c
  ../../../app/src/modules/network/network.c
  ../../../app/src/common/message_channel.c
)

zephyr_include_directories(${ZEPHYR_BASE}/include/zephyr/)
zephyr_include_directories(${ZEPHYR_BASE}/subsys/testsuite/include)
zephyr_include_directories(../../../app/src/common)


target_link_options(app PRIVATE --whole-archive)
# Options that cannot be passed through Kconfig fragments
target_compile_definitions(app PRIVATE
	-DCONFIG_APP_PAYLOAD_CHANNEL_BUFFER_MAX_SIZE=100
	-DCONFIG_APP_NETWORK_LOG_LEVEL=4
	-DCONFIG_APP_NETWORK_THREAD_STACK_SIZE=1024
	-DCONFIG_APP_NETWORK_MESSAGE_QUEUE_SIZE=5
	-DCONFIG_APP_NETWORK_EXEC_TIME_SECONDS_MAX=1
	-DCONFIG_APP_NETWORK_WATCHDOG_TIMEOUT_SECONDS=2
	-DCONFIG_NET_MGMT_EVENT
)

# generate decoder using zcbor
set(zcbor_command
	zcbor code # Invoke code generation
	--cddl ${ZEPHYR_BASE}/subsys/net/lib/lwm2m/lwm2m_senml_cbor.cddl
	--cddl ${APPLICATION_SOURCE_DIR}/../../../app/src/modules/network/conn_info.cddl
	--encode # Generate encoding functions
	--decode # Generate decoding functions
	--short-names # Attempt to make generated symbol names shorter (at the risk of collision)
	-t conn-info-object # Name of the top-level CDDL object
	--output-cmake conn_info_object.cmake # The generated cmake file will be placed here
)
execute_process(COMMAND ${zcbor_command}
				WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
				COMMAND_ERROR_IS_FATAL ANY)

# Include the cmake file generated by zcbor. It adds the
# generated code and the necessary zcbor C code files.
include(${CMAKE_CURRENT_BINARY_DIR}/conn_info_object.cmake)

# Ensure that the cmake reconfiguration is triggerred everytime the cddl file changes.
# This ensures that the codec generation is triggered.
set_property(
	DIRECTORY
	PROPERTY
	CMAKE_CONFIGURE_DEPENDS ${APPLICATION_SOURCE_DIR}/../../../app/src/modules/network/conn_info.cddl
)

zephyr_link_libraries(conn_info_object)
target_link_libraries(conn_info_object PRIVATE zephyr_interface)
